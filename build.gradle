buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'com.github.johnrengelman.shadow'

version = "${mc_version}-${mod_version}"
group = 'ml.denisd3d'
archivesBaseName = 'minecraft2discord'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

repositories {
    jcenter()
}

minecraft {
    mappings channel: 'snapshot', version: '20200514-1.16'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run_client')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                minecraft2discord {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run_server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                minecraft2discord {
                    source sourceSets.main
                }
            }
        }
    }
}


dependencies {
    minecraft ("net.minecraftforge:forge:${mc_version}-${forge_version}")

    // Java Discord library
    compile shadow('net.dv8tion:JDA:4.1.1_162') {
        exclude module: 'opus-java'
    }

    compile shadow ("org.slf4j:slf4j-simple:1.7.28")
    compile shadow ("club.minnced:discord-webhooks:0.3.1")
    compile shadow ('com.vdurmont:emoji-java:5.1.1')
}

shadowJar {
    relocate 'org.apache.commons.collections4', 'ml.denisd3d.repack.org.apache.commons.collections4'
    relocate 'gnu.trove', 'ml.denisd3d.repack.gnu.trove'
    relocate "com.neovisionaries.ws.client", "ml.denisd3d.repack.com.neovisionaries.ws.client"
    relocate "com.iwebpp.crypto", "ml.denisd3d.repack.com.iwebpp.crypto"
    relocate "net.dv8tion.jda", "ml.denisd3d.repack.net.dv8tion.jda"
    relocate "org.json", "ml.denisd3d.repack.org.json"
    relocate "okio", "ml.denisd3d.repack.okio"
    relocate "okhttp3", "ml.denisd3d.repack.okhttp3"
    relocate "org.slf4j", "ml.denisd3d.repack.org.slf4j"
    relocate "com.fasterxml.jackson", "ml.denisd3d.repack.com.fasterxml.jackson"
    relocate "club.minnced.discord", "ml.denisd3d.repack.club.minnced.discord"

    relocate "com.vdurmont.emoji", "ml.denisd3d.repack.com.vdurmont.emoji"

    classifier ''
    configurations = [project.configurations.shadow]
    dependencies{
        exclude(dependency('org.jetbrains:annotations'))
        exclude(dependency('com.google.code.findbugs:jsr305'))
    }
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

jar {
    classifier "slim"
    manifest {
        attributes([
            "Specification-Title": "Minecraft2Discord",
            "Specification-Vendor": "Denis3D",
            "Specification-Version": "${version}",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Denis3D",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.build.dependsOn tasks.shadowJar
