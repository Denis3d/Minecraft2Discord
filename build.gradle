import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

group = 'ml.denisd3d'
version = '3.0.8'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: "com.github.johnrengelman.shadow"

    group = project.group
    version = project.version

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
        maven { url "https://libraries.minecraft.net" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencies {
        shadow group: 'com.discord4j', name: 'discord4j-core', version: '3.2.0-SNAPSHOT'
        shadow group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'
        shadow group: 'com.vdurmont', name: 'emoji-java', version: '5.1.1'
        shadow group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.3.3'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'

        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
        compile(group: 'io.netty', name: 'netty-all', version: '4.1.56.Final') {
            force = true
        } // To provide compatibility in dev mode

        compile group: 'com.electronwill.night-config', name: 'toml', version: '3.6.0' // Included in forge

        configurations.runtime.extendsFrom(configurations.shadow)
    }

    jar {
        archiveClassifier.set('slim')

        from project(":m2d-core").sourceSets.main.output
    }

    shadowJar {
        configurations = [project.configurations.shadow]
        archiveClassifier.set('')

        from project(":m2d-core").sourceSets.main.output
    }

    task relocateShadowJar(type: ConfigureShadowRelocation) {
        target = tasks.shadowJar
        prefix = "ml.denisd3d.repack"
    }

    tasks.shadowJar.dependsOn tasks.relocateShadowJar
    tasks.build.dependsOn tasks.shadowJar
}
