buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = rootProject.version
group = 'ml.denisd3d.mc2discord'
archivesBaseName = 'mc2discord-forge-1.16.x'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

mixin {
    add sourceSets.main, "mc2discord.refmap.json"
}

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        server {
            workingDirectory project.file('run_server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            arg "-mixin.config=mc2discord.mixins.json"
            arg "nogui"

            mods {
                minecraft2discord {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.0.42'

    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'

    compile project(path: ':m2d-core')
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Mc2Discord",
                "Specification-Vendor"    : "DenisD3D",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "DenisD3D",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),

                "MixinConfigs"            : "mc2discord.mixins.json"
        ])
    }
}


reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

jar.finalizedBy('reobfJar')